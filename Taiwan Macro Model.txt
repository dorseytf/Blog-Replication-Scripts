MODEL

COMMENT> Real Private Consumption
BEHAVIORAL> C
TSRANGE 1997 4 2016 4
EQ> C = c1 + c2*TSLAG(C, 1) + c3*(YDD_nom/PGDP) + c4*(STKVLE_nom/CPI)
COEFF> c1 c2 c3 c4
ERROR> AUTO(1)

COMMENT> Real Private Fixed Investment
BEHAVIORAL> IBF
TSRANGE 1997 4 2016 4
EQ> IBF = ibf1 + ibf2*TSLAG(IBF, 1) + ibf3*TSLAG(IG, 1) + ibf4*(TDR1Y - TSDELTAP(CPI)) + ibf5*(STKVLE_nom/CAPSTOCK_nom) + ibf6*SPIKE2008
COEFF> ibf1 ibf2 ibf3 ibf4 ibf5 ibf6
ERROR> AUTO(1)

COMMENT> Real Exports of Goods
BEHAVIORAL> EXG
TSRANGE 1997 4 2016 4
EQ> EXG = exg1 + exg2*(PX/(WPX * RX)) + exg3*GDPMAJOR + exg4*ICSALES_nom + exg5*STEP2008 + exg6*Q1 + exg7*Q2
COEFF> exg1 exg2 exg3 exg4 exg5 exg6 exg7

COMMENT> Real Exports of Services
BEHAVIORAL> EXS
TSRANGE 1997 4 2016 4
EQ> EXS = exs1 + exs2*TSLAG(EXS, 1) + exs3*OVERSEAREV_nom + exs4*VISITORS + exs5*RX + exs6*Q2 + exs7*Q4
COEFF> exs1 exs2 exs3 exs4 exs5 exs6 exs7

COMMENT> Real Imports of Goods
BEHAVIORAL> IMG
TSRANGE 1997 4 2016 4
EQ> IMG = img1 + img2*EXG + img3*C + img4*(IPC + IBF + JJ)
COEFF> img1 img2 img3 img4

COMMENT> Real Imports of Services
BEHAVIORAL> IMS
TSRANGE 1997 4 2016 4
EQ> IMS = ims1 + ims2*TSLAG(IMS, 1) + ims3*OUTBOUND
COEFF> ims1 ims2 ims3
ERROR> AUTO(1)

BEHAVIORAL> PM
TSRANGE 1997 4 2016 4
EQ> PM = pm1 + pm2*RX + pm3*JPNRX + pm4*NONFUEL + pm5*POIL + pm6*WPX
COEFF> pm1 pm2 pm3 pm4 pm5 pm6

BEHAVIORAL> PX
TSRANGE 1997 4 2016 4
EQ> PX = px1 + px2*TSLAG(PX, 1) + px3*RX + px4*PM + px5*WTVG
COEFF> px1 px2 px3 px4 px5
ERROR> AUTO(1)

BEHAVIORAL> PD
TSRANGE 1997 4 2016 4
EQ> PD = pd1 + pd2*TSLAG(PD, 1) + pd3*PM + pd4*GDP + pd5*SPIKE2008
COEFF> pd1 pd2 pd3 pd4 pd5
ERROR> AUTO(1)

BEHAVIORAL> PF
TSRANGE 1997 4 2016 4
EQ> PF = pf1 + pf2*TSLAG(PF, 1) + pf3*FOOD + pf4*TSLAG(WG_nom, 1) + pf5*TYPH_T + pf6*Q1
COEFF> pf1 pf2 pf3 pf4 pf5 pf6
ERROR> AUTO(1)

BEHAVIORAL> PO
TSRANGE 1997 4 2016 4
EQ> PO = po1 + po2*TSLAG(PO, 1) + po3*TSLAG(PM, 1) + po4*PM + po5*WG_nom + po6*Q1 + po7*Q2 + po8*Q3
COEFF> po1 po2 po3 po4 po5 po6 po7 po8
ERROR> AUTO(1)

BEHAVIORAL> PC
TSRANGE 1997 4 2016 4
EQ> PC = pc1 + pc2*PF + pc3*PO
COEFF> pc1 pc2 pc3

BEHAVIORAL> PCG
TSRANGE 1997 4 2016 4
EQ> PCG = pcg1 + pcg2*TSLAG(PCG, 1) + pcg3*PO + pcg4*WGP + pcg5*PIG + pcg6*PD
COEFF> pcg1 pcg2 pcg3 pcg4 pcg5 pcg6

BEHAVIORAL> PIBF
TSRANGE 1997 4 2016 4
EQ> PIBF = pibf1 + pibf2*TSLAG(PIBF, 1) + pibf3*PCON + pibf4*PCA + pibf5*WGST_nom + pibf6*SPIKE2008 + pibf7*Q1 + pibf8*Q2
COEFF> pibf1 pibf2 pibf3 pibf4 pibf5 pibf6 pibf7 pibf8
ERROR> AUTO(1)

BEHAVIORAL> PIG
TSRANGE 1997 4 2016 4
EQ> PIG = pig1 + pig2*TSLAG(PIG, 1) + pig3*PCON + pig4*WGST_nom + pig5*SPIKE2008
COEFF> pig1 pig2 pig3 pig4 pig5 

BEHAVIORAL> PIPC
TSRANGE 1997 4 2016 4
EQ> PIPC = pipc1 + pipc2*PCON + pipc3*PCA + pipc4*SPIKE2008
COEFF> pipc1 pipc2 pipc3 pipc4 

BEHAVIORAL> PGCF
TSRANGE 1997 4 2016 4
EQ> PGCF = pgcf1 + pgcf2*PCON + pgcf3*PCA + pgcf4*SPIKE2008
COEFF> pgcf1 pgcf2 pgcf3 pgcf4 

BEHAVIORAL> PEX
TSRANGE 1997 4 2016 4
EQ> PEX = pex1 + pex2*PX + pex3*CPI
COEFF> pex1 pex2 pex3

BEHAVIORAL> PIM
TSRANGE 1997 4 2016 4
EQ> PIM = pim1 + pim2*PM + pim3*CPI
COEFF> pim1 pim2 pim3

BEHAVIORAL> M1B
TSRANGE 1997 4 2016 4
EQ> M1B = m1b1 + m1b2*CPI + m1b3*GDP + m1b4*(VSTOCK_nom/CPI) + m1b5*(TDR1Y - NOTERATE)
COEFF> m1b1 m1b2 m1b3 m1b4 m1b5
ERROR> AUTO(1)

BEHAVIORAL> TDR1Y
TSRANGE 1997 4 2016 4
EQ> TDR1Y = tdr1 + tdr2*REQ + tdr3*IR + tdr4*TSLAG(GDP, 1) + tdr5*Q3
COEFF> tdr1 tdr2 tdr3 tdr4 tdr5
ERROR> AUTO(1)

BEHAVIORAL> STKVLE_nom
TSRANGE 1997 4 2016 4
EQ> STKVLE_nom = stk1 + stk2*M1B + stk3*VSTOCK_nom + stk4*SPIKE2009
COEFF> stk1 stk2 stk3 stk4

BEHAVIORAL> NE
TSRANGE 1997 4 2016 4
EQ> NE = ne1 + ne2*TSLAG(NE, 1) + ne3*GDP 
COEFF> ne1 ne2 ne3
ERROR> AUTO(1)

BEHAVIORAL> WG_nom
TSRANGE 1997 4 2016 4
EQ> WG_nom = wg1 + wg2*MINWGM + wg3*(GDP_nom - DEP_nom) + wg4*Q1 
COEFF> wg1 wg2 wg3 wg4 

BEHAVIORAL> TAX_nom
TSRANGE 1997 4 2016 4
EQ> TAX_nom = tax1 + tax2*TSLAG(GDP_nom, 1) + tax3*(EX_nom + C_nom) + tax4*VSTOCK_nom + tax5*Q1 + tax6*Q2
COEFF> tax1 tax2 tax3 tax4 tax5 tax6
ERROR> AUTO(1)

IDENTITY> C_nom
EQ> C_nom = C * (PC/100)

IDENTITY> CG_nom
EQ> CG_nom = COMM_SERVICES + CULTURE + ECON_AFFAIRS + EDU + ENVIRO_PROTECT + GEN_PUB_AFFAIRS + HLTH_INS + NATL_DEFENSE + PUB_SAFETY + SOC_SECURITY

IDENTITY> CG
EQ> CG = (CG_nom/PCG) * 100

IDENTITY> IG
EQ> IG = (IG_nom/PIG) * 100

IDENTITY> IPC
EQ> IPC = (IPC_nom/PIPC) * 100

IDENTITY> IBF_nom
EQ> IBF_nom = IBF * (PIBF/100)

IDENTITY> GCF_nom
EQ> GCF_nom = IG_nom + IPC_nom + IBF_nom + JJ_nom

IDENTITY> GCF
EQ> GCF = (GCF_nom/PGCF) * 100

IDENTITY> EX
EQ> EX = TSLAG(EX, 1) * (1 + ( ((TSLAG(EXG_nom,1)/TSLAG(GDP_nom,1))*TSDELTAP(EXG_nom) + (TSLAG(EXS_nom,1)/TSLAG(GDP_nom,1))*TSDELTAP(EXS_nom)) / ((TSLAG(EX_nom,1)/TSLAG(GDP_nom,1))*100) ))

IDENTITY> IM
EQ> IM = TSLAG(IM, 1) * (1 + ( ((TSLAG(IMG_nom,1)/TSLAG(GDP_nom,1))*TSDELTAP(IMG_nom) + (TSLAG(IMS_nom,1)/TSLAG(GDP_nom,1))*TSDELTAP(IMS_nom)) / ((TSLAG(IM_nom,1)/TSLAG(GDP_nom,1))*100) ))

IDENTITY> EX_nom
EQ> EX_nom = EX * (PEX/100)

IDENTITY> IM_nom
EQ> IM_nom = IM * (PIM/100)

IDENTITY> EXG_nom
EQ> EXG_nom = EX_nom * EX_nom_wg

IDENTITY> IMG_nom
EQ> IMG_nom = IM_nom * IM_nom_wg

IDENTITY> EXS_nom
EQ> EXS_nom = EX_nom - EXG_nom

IDENTITY> IMS_nom
EQ> IMS_nom = IM_nom - IMG_nom

IDENTITY> PCH_GDP
EQ> PCH_GDP = TSDELTAP(GDP)

IDENTITY> GDP
EQ> GDP = C + CG + GCF + EX - IM

IDENTITY> GDP_nom
EQ> GDP_nom = C_nom + CG_nom + GCF_nom + EX_nom - IM_nom

IDENTITY> PGDP
EQ> PGDP = (GDP_nom/GDP) * 100

IDENTITY> YDD_nom
EQ> YDD_nom = GNI_nom - DEP_nom - TAX_nom

IDENTITY> GNI_nom
EQ> GNI_nom = GDP_nom + FIA_nom

IDENTITY> CPI
EQ> CPI = CPI_base * (0.2972 * (PF/PF_base) + 0.7027 * (PO/PO_base))

END
